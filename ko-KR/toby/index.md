---
title: 토비
level: Scratch +
language: ko-KR
stylesheet: scratch
embeds: "*.png"
materials: "*.sb2"
...

# 소개 { .intro}

이 프로젝트에선 개, 토비가 공이 바닥에 떨어지는 걸 막으면서 5개의 치즈퍼프 과자 그릇을 얻는게 목표인 만들 것입니다. 
토비가 공을 두개 이상 떨어뜨리면 게임이 끝납니다. 문제는 과연 당신은 모든 공들을 공중에 띄워 둘 수 있을까요?

<div class="scratch-preview">
	<iframe allowtransparency="true" width="485" height="402" src="http://scratch.mit.edu/projects/embed/49677948/?autostart=false" frameborder="0"></iframe>
	<img src="Toby_dog.png">
</div>


# **1 단계:** 토비 조종하기 {.activity}

## 단계별 체크리스트 { .check}

+ 새 스크래치 프로제트를 시작하고 고양이에 오른쪽 클릭을 한 후 '삭제'를 눌러서 지우세요.

+ 스테이지를 클릭하세요. 스크래치 저장소에서 배경을 골라보세요: 이 프로젝트엔 **night city with street** 도 잘 어울립니다. 기본 텅 빈 배경은 지우세요.

+ **Dog2** 스프라이트를 **Animals** 란에서 골라서 새 스프라이트를 생성하세요.

+ **Dog2** 는 세가지 모양을 가지고 나옵니다. 세번째 모양인 **dog2-c** 는 필요 없으니 지우세요.

+ 스프라이트 그림의 왼쪽 상단의 파란색 `info` {.blockmotion} 버튼을 눌러서 스프라이트의 이름을 __Toby__ 로 지으세요.

+ 아래 사진과 같이 `회전 방식을(왼쪽-오른쪽)로 정하기` {.blockmotion}를 선택하세요:

	![screenshot](Toby_Sprite.png)

+ 오른쪽 방향키를 누르면 토비가 오른쪽으로 가야 합니다. 스크립트 탭을 눌러서 이 스크립트를 넣으세요:

```blocks

깃발 클릭했을 때
배경을 [night city with street v] (으)로 바꾸기
무한 반복하기
   만약 <[오른쪽 화살표 v] 키를 눌렀는가?> 라면
      (90 v) 도 방향 보기
      (10) 만큼 움직이기
      다음 모양으로 바꾸기
   end
end
```


+ 녹색 깃발을 눌러서 토비가 오른쪽 방향키를 누르면 오른쪽으로 움직이는지 확인하세요 이제 토비가 왼쪽으로도 움직일 수 있게 코드를 더 더해 넣읍시다:

```blocks

깃발 클릭했을 때
배경을 [night city with street v] (으)로 바꾸기
무한 반복하기
   만약 <[오른쪽 화살표 v] 키를 눌렀는가?> 라면
      (90 v) 도 방향 보기
      (10) 만큼 움직이기
      다음 모양으로 바꾸기
   end
   만약 <[왼쪽 화살표 v] 키를 눌렀는가?> 라면
      (-90 v) 도 방향 보기
      (10) 만큼 움직이기
      다음 모양으로 바꾸기
   end
end
```

## 프로젝트를 확인해보세요 { .flag}
 
__녹색 깃발을 클릭하세요__, 왼쪽 방향키를 눌러보세요. 토비가 왼쪽으로 가나요? 오른쪽도 확인해보세요!


## 프로젝트를 저장하세요 { .save}

# **2 단계:** 토비를 바쁘게 하기! {.activity}

이제 토비가 움직일 수 있으니 할 일을 줍시다. 게임에서 이기려면 토비는 5개의 치즈 퍼프 그릇을 얻어야 합니다. 그릇은 무작위 시간과 장소에 나타나야 합니다.

## 단계별 체크리스트 { .check}

+ 저장소에서 새로운 스프라이트를 더해주세요: **Things** 카테고리에서 **cheesy-puffs** 를 선택하시고, **bowl**로 이름을 변경해주세요. 

+ __모양__ 탭을 클릭하고 치즈 퍼프 그릇의 크기를 위쪽 가위 아이콘 가까이에 있는 **축소** 아이콘을 눌러서 줄여주세요. 그릇 스프라이트를 클릭해야 작아집니다. 축소 아이콘은 이렇게 생겼습니다:

	![screenshot](shrink.png)


+ 다른 방법으로 그릇 크기를 줄였다면 코스튬 중심을 리셋 해서 그릇의 중심부에 있게 만들어야 할 겁니다. 그러려면 오른쪽 상단의 **모양 중심 설정** 아이콘을 눌러서 십자선을 아래 사진과 같이 움직여주세요:  

	![screenshot](Set_centre.png)


+ **cheesy-puffs**는 길바닥에 무작위 장소에 나타나야 합니다.`y 좌표` {.blockmotion}는 그대로 두지만, `x 좌표` {.blockmotion}는 그릇이 무작위 장소에 나타나도록 비껴야 합니다. 다음 스크립트를 **bowl** 스프라이트에 더해주세요: 

```blocks

깃발 클릭했을 때
무한 반복하기
   (5) 초 기다리기
   x:((-220) 부터 (220) 사이의 난수) y:(-140) 로 이동하기
end
```

+ 녹색 깃발을 클릭하고 잠시 기다려주세요. 치즈 퍼프 그릇이 5초마다 다른 곳으로 옮겨 가나요?

+ 지금으로선 게임이 너무 예상하기 쉽습니다! 가끔 그릇이 숨겨지도록 해서 난이도를 더해 줍시다 스크립트를 다음가 같이 수정해주세요:

```blocks

깃발 클릭했을 때
숨기기
무한 반복하기
   ((1) 부터 (10) 사이의 난수) 초 기다리기
   x:((-220) 부터 (220) 사이의 난수) y:(-140) 로 이동하기
   보이기
   ((1) 부터 (10) 사이의 난수) 초 기다리기
   숨기기
end
```

## 프로젝트를 확인해주세요 { .flag}


__녹색 깃발을 클릭해주세요__, 이제 치즈 퍼프 그릇이 무작위의 시간동안 무작위 장소로 나타날 것입니다.

## 프로젝트를 저장해주세요 { .save }

# **3 단계:** 그릇 모으기 {.activity }

게임의 승리조건은 토비가 5개의 치즈 퍼프 그릇을 모으는 것입니다. 

## 단계별 체크리스트 { .check}

+ `bowls` {.blockdata} 변수를 만들어서 토비가 지금까지 몇개의 그릇을 모았는지 확인하도록 하세요.

	![screenshot](bowls.png)

+ 이제 토비가 몇개의 그릇을 모았는지 세어 줄 코드를 넣어야 합니다. 토비가 치즈 퍼프 그릇을 건드릴 때 마다 `bowls` {.blockdata} 변수가 1씩 올라가야 합니다. 다음 스크립트를 치즈 퍼프 그릇 스크립트에 더해주세요. (예전 스크립트도 남겨 두세요! 양쪽 스크립트 다 필요합니다!)

```blocks

깃발 클릭했을 때
[bowls v] 에 (0) 저장하기
무한 반복하기
   만약 <[Toby v] 에 닿기?> 라면
      [bowls v] 을(를) (1) 만큼 바꾸기
      (0.5) 초 기다리기
      숨기기
   end
end
```
 
+ 모인 그릇 수가 5가 되면 게임이 멈춰야 됩니다. 다른 스크립트들과 배경에 메시지 `won` {.blockevents} 을 날려서 플레이어가 이겼다는 것을 알려줄 것입니다.
두번째 스크립트를 이렇게 수정하세요:

```blocks

깃발 클릭했을 때
[bowls v] 에 (0) 저장하기
무한 반복하기
   만약 <[Toby v] 에 닿기?> 라면
      [bowls v] 을(를) (1) 만큼 바꾸기
      만약 <(bowls) = (5)> 라면
         [won v] 방송하기
         [스프라이트에 있는 다른 스크립트 v] 멈추기
      end
      (0.5) 초 기다리기
      숨기기
   end
end
```

+ 토비에게 작은 스크립트를 더해 `won` {.blockevents} 메시지를 받으면 '잘 했어요!"라고 말하게 합시다:

```blocks

[won v] 수신할 때
[잘 했어요!] 을(를) (2) 초동안 말하기
```

## 프로젝트를 확인해 보세요 { .flag}

__녹색 깃발을 클릭해 보세요__, `bowls` {.blockdata} 변수가 토비가 그릇을 모을 때마다 올라가나요? 토비가 다섯 그릇을 다 모으며 "well done" 이라고 하나요?

## 프로젝트를 저장해 주세요 { .save }

## 도전과제 1: 음향효과 더하기 { .challenge}

+ 코드를 더해 그릇이 모일때마다 **pop** 소리가 나게 할 수 있나요?

+ 배경에 짤막한 스크립트를 더해 플레이어가 승리했을 때 배경이 바뀌고 음악이 나오도록 할 수 있나요? 

힌트: 스테이지에 더할 스크립트는 `(won)을(를) 받았을 때` {.blockevents} 블록으로 시작해야 할 것입니다.
	

## 프로젝트를 저장하세요 { .save }

# **4 단계:** 튕기는 공을 더해 넣기 {.activity }

튕기는 공 요소를 넣어서 게임을 더 흥미롭게 만들어 봅시다!
토비가 공도 받아내게 만듭시다. 토비가 치즈 퍼프 그릇 5개를 모으기 전에 공 3개를 떨어뜨린다면 게임에서 집니다!

## 단계별 체크리스트 { .check}

+ 새 스프라이트를 넣으세요: 스크래치 저장소에서 **Beachball** 을 선택하세요.

+ **Beachball**의 크기를 줄이세요. 코스튬의 중심점이 제대로 잡혀 있도록 하세요, 공의 정확히 중심에 오도록요. **모양**탭 오른쪽 상단에서 **모양 중심 정하기** 아이콘을 클릭해서 확인할 수 있습니다. 공의 움직임에 영향을 주니 중심정을 정확히 잡는건 매우 중요합니다.

+ 이제 이 공이 하늘에서 떨어지면서 마구 튕기도록 해야합니다. 다음 스크립트를 공에 더해주세요:

```blocks

깃발 클릭했을 때
보이기
x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
(135 v) 도 방향 보기
무한 반복하기
   (4) 만큼 움직이기
   벽에 닿으면 튕기기
end
```

## 프로젝트를 확인해주세요 { .flag}

__녹색 깃발을 클릭하세요__, 공이 하늘에서 떨어지면서 배경의 가장자리에 튕겨 나갈 것입니다. 공을 더 빠르 게나 더 느리게 튕기게 하려면 어떤 숫자를 수정해야 할까요?

## 프로젝트를 저장해주세요 { .save }

# **5 단계:** 더 튀기게 만들자 {.activity }

문제는 토비가 공을 건드려도 아무 일도 안 일어난다는 것입니다. 이걸 고쳐보죠! 

## 단계별 체크리스트 { .check}

+ 스크립트를 수정해서 공이 토비에게 닿으면 튀기게 만듭시다:

```blocks

깃발 클릭했을 때
보이기
x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
(135 v) 도 방향 보기
무한 반복하기
   (4) 만큼 움직이기
   벽에 닿으면 튕기기
   만약 <[Toby v] 에 닿기?> 라면
      오른쪽으로 ((90) 부터 (270) 사이의 난수) 도 돌기
      (100) 만큼 움직이기
   end
end
```

+ 또한 공이 언제 바닥에 닿았는지를 확인할 코드를 더해야 합니다. 공은 `y 좌표` {.blockmotion}가 140 이하일때 바닥에 닿은 듯이 보입니다. 이건 대략적인 숫자이며 특히 배경을 다른 곳으로 정했다면 조정해야 할 것입니다. 
이제 스크립트를 수정해서 공이 떨어지자 마자 다시 위로 올라가게(또한 색도 바뀌게) 만들 것입니다. **water drop** 음향효과를 업로드하고 스크립트를 다시 수정하세요:

```blocks

깃발 클릭했을 때
보이기
x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
(135 v) 도 방향 보기
무한 반복하기
   (4) 만큼 움직이기
   벽에 닿으면 튕기기
   만약 <[Toby v] 에 닿기?> 라면
      오른쪽으로 ((90) 부터 (270) 사이의 난수) 도 돌기
      (100) 만큼 움직이기
   end
   만약 <(y좌표) < (-140)> 라면
      [색깔 v] 효과를 (25) 만큼 바꾸기
      x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
      [water drop v] 소리내기
   end
end
	```

## 프로젝트를 확인해보세요 { .flag}

__녹색 깃발을 클릭하세요__, 공이 떨어졌을 때 어떻게 되나요?
새로운 공이 떨어지는 걸 볼 수 있나요? 다른 색깔인가요?


## 프로젝트를 저장하세요 { .save}


# **6 단계:** 떨어진 공 세기  {.activity}

## 단계별 체크리스트 { .check}

+ 이제 토비가 몇개의 공을 떨어뜨렸는지 확인할 수 있게 해야합니다. 그러려면 모든 스프라이트에서 사용하는 `droppedBalls` {.blockdata}라는 변수를 만드세요.

	![screenshot](droppedBalls.png)

+ 게임을 시작했을 땐 토비는 공을 하나도 떨어뜨리지 않았습니다. 그러니 `droppedBalls` {.blockdata} 변수도 0으로 초기화 해야합니다. 공이 바닥에 닿을 때 마다 `droppedBalls` {.blockdata} 변수가 1씩 늘어나야 됩니다.
떨어뜨린 공의 숫자가 3 이상이면 게임에서 집니다. 그러니 `gameover` {.blockevents}이란 메시지를 다른 스프라이트에 보내 게임이 끝났고 플레이어가 졌다는 걸 알립니다. **Beachball** 스크립트는 이제 이래야 합니다: 

```blocks

깃발 클릭했을 때
[droppedBalls v] 에 (0) 저장하기
보이기
x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
(135 v) 도 방향 보기
무한 반복하기
   (4) 만큼 움직이기
   벽에 닿으면 튕기기
   만약 <[Toby v] 에 닿기?> 라면
      오른쪽으로 ((90) 부터 (270) 사이의 난수) 도 돌기
      (100) 만큼 움직이기
   end
   만약 <(y좌표) < (-140)> 라면
      [droppedBalls v] 을(를) (1) 만큼 바꾸기
      만약 <(droppedBalls) = (3)> 라면
         [gameOver v] 방송하기
         숨기기
         [이 스크립트 v] 멈추기
      end
      [색깔 v] 효과를 (25) 만큼 바꾸기
      x:((-220) 부터 (220) 사이의 난수) y:(160) 로 이동하기
      [water drop v] 소리내기
   end
end
```

+ 플레이어가 승리하면 게임이 멈추고 공은 그만 튀기고 숨겨져야 합니다:

```blocks

[won v] 수신할 때
숨기기
[스프라이트에 있는 다른 스크립트 v] 멈추기
```


+ **bowl** 스프라이트에 이 스크립트를 더해 게임이 끝났을 때 그만 나오도록 하세요:

```blocks

[gameOver v] 수신할 때
[스프라이트에 있는 다른 스크립트 v] 멈추기
```

+ 스테이지를 클릭하고 Sounds 탭을 클릭해서 **spooky string** 와 **triumph** 음향효과를 스크래치 저장소에서 업로드 하세요. 

+ 이 스크립트를 스테이지에 더해 게임에 졌을 때 **spooky string** 음향효과가 나오게 하세요:

```blocks

[gameOver v] 수신할 때
[색깔 v] 효과를 (25) 만큼 바꾸기
[spooky string v] 소리내기
```

+ 이 스크립트를 스테이지에 더해 게임에서 이기면 화면 색이 몇 번 바뀌고 **triumph** 음향효과가 나오게 하세요:

```blocks

[won v] 수신할 때
[triumph v] 소리내기
(12) 번 반복하기
   [색깔 v] 효과를 (25) 만큼 바꾸기
   (0.5) 초 기다리기
end
```
+ 마지막으로 이 스크립트를 더해 게임을 재시작 하면 스테이지의 색이 원래대로 돌아오게 만드세요:

```blocks

깃발 클릭했을 때
그래픽 효과 지우기
```

## 프로젝트를 확인해보세요 { .flag}

__녹색 깃발을 클릭해 주세요__, `droppedBalls` {.blockdata} 변수가 공이 떨어질 때마다 늘어나나요? 생각나는 최대한 많은 방법으로 게임 작동을 확인해보세요 (예를 들면 패배와 승리를 확인해본다 던지) 예상대로 작동하나요?

  
## 프로젝트를 저장하세요 { .save}


## 도전과제 2: 게임을 더 어렵게 만들어보세요 { .challenge}

게임을 어떻게 더 어렵게 할 수 있을까요? 몇가지 예시입니다:
+ 그릇이 더 빨리, 더 오래 사라지도록 하세요. 어느 수치를 수정해야 되나요?  
+ 공을 더 빠르게 움직이거나 다른 종류의 공을 더해보세요: 이번엔 농구공일수도 있겠죠. 비치 볼에서 스크립트를 드래그 해와 복사해 넣을 수 있습니다. 다시 스크립트를 통째로 쓸 필요는 없어요...
+ 게임에 다른 배경과 더 많은 공이 있는 2 단계 난이도를 만들 수 있습니다. 이번엔 치즈 퍼프 그릇대신 도넛을 모아야 할 수도 있습니다(스크래치 저장소에 **donut** 스프라이트가 있습니다). `level` {.blockdata} 변수를 만들어서 몇 단계의 난이도를 플레이 하고 있는지 확인할 수도 있습니다.

           
## 프로젝트를 저장하세요 { .save}

수고하셨습니다, 이제 끝났어요! 게임을 즐겨주세요!

친구들과 가족들에게 **공유하기** 버튼을 눌러서 함께 할 수 있다는 걸 잊지 마세요!
